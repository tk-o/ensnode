locals {
  mount_path           = "/app/apps/ensrainbow/data"
  resource_name_suffix = var.ensrainbow_label_set_id
  fqdn                 = "${var.subdomain_path}.${var.ensnode_environment_name}.${var.hosted_zone_name}"
}

# For details on "render_web_service", see:
# https://registry.terraform.io/providers/render-oss/render/latest/docs/resources/web_service
resource "render_web_service" "ensrainbow" {
  name           = "ensrainbow-${local.resource_name_suffix}"
  plan           = var.render_instance_plan
  region         = var.render_region
  environment_id = var.render_environment_id

  runtime_source = {
    image = {
      image_url = "ghcr.io/namehash/ensnode/ensrainbow"
      tag       = var.ensnode_version
    }
  }

  disk = {
    name       = "ensrainbow-data-${local.resource_name_suffix}"
    size_gb    = var.disk_size_gb
    mount_path = local.mount_path
  }

  env_vars = {
    "LOG_LEVEL"         = { value = "error" }
    "DOWNLOAD_TEMP_DIR" = { value = "${local.mount_path}/tmp" },
    "LABEL_SET_ID"      = { value = var.ensrainbow_label_set_id }
    "LABEL_SET_VERSION" = { value = var.ensrainbow_label_set_version }
    "DB_SCHEMA_VERSION" = { value = var.db_schema_version }
  }

  # See https://render.com/docs/custom-domains
  custom_domains = [
    { name : local.fqdn },
  ]
}
